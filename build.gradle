//plugins {
//    id 'digital.wup.android-maven-publish' version '3.0.0' apply false
//}

version = "1.0.0-SNAPSHOT"

subprojects {
    if (!it.name.contains('android')) {
        apply plugin: 'java'
        apply plugin: 'maven-publish'

        apply from: "$rootDir/gradle/quality.gradle"

        repositories {
            mavenCentral()
        }

        jar {
            manifest {
                attributes(
                        "Implementation-Title": project.name,
                        "Implementation-Version": rootProject.version
                )
            }
        }
    }

//    if(!it.name.startsWith('examples') && it.name.contains('android')) {
//        apply plugin: 'digital.wup.android-maven-publish'
//
//        publishing {
//            publications {
//                mavenAar(MavenPublication) {
//                    println components
//                    //from components.android
//                }
//            }
//        }
//    }

    if(!it.name.startsWith('examples') && !it.name.contains('android')) {
        publishing {
            task sourcesJar(type: Jar, dependsOn: classes) {
                classifier = 'sources'
                from sourceSets.main.allSource
            }

            task javadocJar(type: Jar, dependsOn: javadoc) {
                classifier = 'javadoc'
                from javadoc.destinationDir
            }

            publications {
                maven(MavenPublication) {
                    groupId 'com.rollbar'
                    version rootProject.version

                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }
    }

    if (!it.name.contains('android')) {
        dependencies {
            testCompile group: 'junit', name: 'junit', version: '4.11'
            testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
            testCompile group: 'org.mockito', name: 'mockito-core', version: '2.10.0'
        }

        compileJava {
            sourceCompatibility = JavaVersion.VERSION_1_7
            targetCompatibility = JavaVersion.VERSION_1_7
            options.bootClasspath = "${System.env.JDK7_HOME}/jre/lib/rt.jar"
        }

        compileTestJava {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
