buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.vanniktech:gradle-maven-publish-plugin:0.33.0"
        classpath "com.github.spotbugs.snom:spotbugs-gradle-plugin:4.8.0"
        classpath "com.palantir.gradle.revapi:gradle-revapi:1.8.0"
    }
}

allprojects {
    version VERSION_NAME

    repositories {
        google()
        mavenCentral()
    }
}

subprojects { project ->
    if (!project.name.contains('android') && !project.parent.name.equals("examples") && !project.name.contains('examples')) {
        apply plugin: 'java-library'

        apply from: "$rootDir/gradle/release.gradle"

        apply from: "$rootDir/gradle/quality.gradle"

        apply from: "$rootDir/gradle/compatibility.gradle"

        repositories {
            mavenCentral()
        }

        jar {
            manifest {
                attributes(
                        "Implementation-Title": project.name,
                        "Implementation-Version": VERSION_NAME
                )
            }
        }

        dependencies {
            testImplementation group: 'junit', name: 'junit', version: '4.13.2'
            testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
            testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.8.0'
        }

        compileJava {
            if (JavaVersion.current().isJava9Compatible()) {
                options.release = 8
            } else {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }

        compileTestJava {
            if (JavaVersion.current().isJava9Compatible()) {
                options.release = 8
            } else {
                sourceCompatibility = JavaVersion.VERSION_1_8
                targetCompatibility = JavaVersion.VERSION_1_8
            }
        }
    }
}

wrapper {
    gradleVersion = '6.9.4'
}
