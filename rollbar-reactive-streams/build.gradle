buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-project-plugin:10.0.1'
    }
}

apply plugin: 'com.netflix.nebula.integtest'

repositories {
    mavenCentral()
}

dependencies {
    api project(':rollbar-java')
    api 'org.reactivestreams:reactive-streams:1.0.3'
    compileOnly 'org.apache.httpcomponents.client5:httpclient5:5.0.3'

    testImplementation 'org.reactivestreams:reactive-streams-tck:1.0.3'
    testImplementation 'org.testng:testng:7.4.0'
    testImplementation 'org.mockito:mockito-testng:0.3.0'
    testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.0.3'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'com.google.code.gson:gson:2.8.2'

    testImplementation platform('io.projectreactor:reactor-bom:2020.0.6')
    testImplementation 'io.projectreactor:reactor-core'

    // We'll reuse some of the tests since we're providing compatible implementations
    testImplementation(project(path: ':rollbar-java', configuration: 'integTestRuntime'))
    testImplementation(project(path: ':rollbar-java', configuration: 'integTestArtifacts'))

    integTestImplementation 'com.github.tomakehurst:wiremock-jre8:2.27.2'
    integTestImplementation 'com.google.code.gson:gson:2.8.2'
}

// The reactive streams TCK tests use TestNG
tasks.register("tckTest", Test) {
    useTestNG()
}

check.dependsOn("tckTest")

tasks.named("integrationTest").configure {
    dependsOn project(':rollbar-java').tasks.named("integTestJar")
}
