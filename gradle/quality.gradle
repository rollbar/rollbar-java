apply plugin: "com.github.spotbugs"
apply plugin: "checkstyle"
apply plugin: "jacoco"


checkstyle {
    toolVersion = "8.2"
    sourceSets = [sourceSets.main]
    configFile = file("$rootDir/tools/checkstyle/google_checks.xml")
}

spotbugs {
    toolVersion = '4.7.3'
    includeFilter = file("$rootDir/tools/findbugs/findbugs.xml")
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required.set(false)
        html.required.set(true)
    }
}

afterEvaluate {
    tasks.matching { it.class.name == "com.github.spotbugs.snom.SpotBugsTask" }.configureEach {
        reports {
            html.required.set(true)
            html.stylesheet = 'fancy-hist.xsl'
            html.outputLocation.set(file("$buildDir/reports/spotbugs/${name}/spotbugs.html"))
        }
    }

    spotbugsTest{
        enabled = false
    }

    if (tasks.findByName("spotbugsIntegTest") != null) {
        spotbugsIntegTest {
            enabled = false
        }
    }
}

jacoco {
    toolVersion = "0.8.8"
}

test {
    jacoco {
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }

    finalizedBy jacocoTestReport // report is always generated after tests run
}
